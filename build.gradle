apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'eclipse-wtp'

repositories {
	mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

def defaultEncoding = 'UTF-8'

configurations {
	executableWar
}

dependencies {
	compile 'commons-dbcp:commons-dbcp:1.4'
	compile 'javax.servlet:javax.servlet-api:3.0.1'
	compile 'ch.qos.logback:logback-classic:1.0.13'
	compile 'org.apache.velocity:velocity:1.7'
	compile 'org.springframework:spring-webmvc:4.0.0.RELEASE'
	compile 'org.springframework:spring-jdbc:4.0.0.RELEASE'
	compile 'org.springframework:spring-context-support:4.0.0.RELEASE'
	compile 'org.springframework.security:spring-security-web:3.2.0.RELEASE'
	compile 'org.springframework.security:spring-security-config:3.2.0.RELEASE'
	
	compile 'org.eclipse.jetty:jetty-io:7.5.4.v20111024'
	compile 'org.eclipse.jetty:jetty-server:7.5.4.v20111024'
	compile 'org.eclipse.jetty:jetty-servlet:7.5.4.v20111024'
	compile 'org.eclipse.jetty:jetty-util:7.5.4.v20111024'
	compile 'org.eclipse.jetty:jetty-webapp:7.5.4.v20111024'
	
	testCompile 'org.springframework:spring-test:4.0.0.RELEASE'
	testCompile 'org.hamcrest:hamcrest-library:1.3'
	testCompile 'org.hamcrest:hamcrest-core:1.3'
	testCompile 'org.mockito:mockito-all:1.9.5'
	testCompile 'junit:junit:4.11'
		
	runtime 'org.slf4j:jcl-over-slf4j:1.7.5'
	runtime 'com.h2database:h2:1.3.173'
	
 	testRuntime 'org.slf4j:jcl-over-slf4j:1.7.5'
	
	executableWar 'org.eclipse.jetty:jetty-webapp:7.5.4.v20111024'
	executableWar 'org.eclipse.jetty:jetty-server:7.5.4.v20111024'
	executableWar 'org.eclipse.jetty:jetty-servlet:7.5.4.v20111024'
}

compileJava {
	options.encoding = defaultEncoding
}

compileTestJava {
	options.encoding = defaultEncoding
}

eclipse {
	classpath {
		downloadSources = true
	}
}

jettyRun {
	httpPort = 9050
}

war {
	from {
		configurations.executableWar.collect {
			return it.isDirectory() ? it : zipTree(it)
		}
	}
	
	from "$buildDir/classes/main"
	exclude('**/META-INF/*.SF', '**/META-INF/*.RSA', '**/META-INF/*.DSA')
	
	manifest {
		attributes "Main-Class" : "security.Main"
	}
}